plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'fr.bmartel'
version = '2.0.0'

def getExtOrDefault(name, defaultValue) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : defaultValue
}

android {
    namespace 'fr.bmartel.speedtest'
    compileSdk getExtOrDefault('compileSdkVersion', 36)

    defaultConfig {
        minSdk getExtOrDefault('minSdkVersion', 28)
        targetSdk getExtOrDefault('targetSdkVersion', 36)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
        warningsAsErrors false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'commons-net:commons-net:3.10.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation 'net.jodah:concurrentunit:0.4.6'
    testImplementation "com.google.code.gson:gson:2.10.1"
    testImplementation 'org.json:json:20231013'
    testImplementation 'org.robolectric:robolectric:4.11.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'fr.bmartel'
            artifactId = 'jspeedtest'
            version = project.version

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'JSpeedTest'
                description = 'Speed Test library for Java/Android'
                url = 'https://github.com/bertrandmartel/speed-test-lib'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/bertrandmartel/speed-test-lib/blob/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id = 'bertrandmartel'
                        name = 'Bertrand Martel'
                        email = 'bmartel.fr@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/bertrandmartel/speed-test-lib.git'
                    developerConnection = 'scm:git:ssh://github.com/bertrandmartel/speed-test-lib.git'
                    url = 'https://github.com/bertrandmartel/speed-test-lib'
                }
            }
        }
    }
}

tasks.withType(Test) {
    enabled = false
}

tasks.register('generateJavadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("${project.projectDir}/doc/")
    failOnError = false
}
